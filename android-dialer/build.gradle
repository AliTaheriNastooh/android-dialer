apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0"       // Semver
group = "io.uuddlrlrba" // Maven Group ID for the artifact
def siteUrl = 'https://github.com/bmaslakov/android-dialer'
def gitUrl = 'https://github.com/bmaslakov/android-dialer.git'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    javadocDeps
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:support-annotations:26.0.1'
    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'com.googlecode.libphonenumber:libphonenumber:8.8.0'
    compile 'com.googlecode.libphonenumber:geocoder:2.77'
    testCompile 'junit:junit:4.12'
    javadocDeps 'com.android.support:support-annotations:26.0.1'
    javadocDeps 'com.android.support:appcompat-v7:26.0.1'
    javadocDeps 'com.android.support:design:26.0.1'
    javadocDeps 'com.googlecode.libphonenumber:libphonenumber:8.8.0'
    javadocDeps 'com.googlecode.libphonenumber:geocoder:2.77'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) + configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = rootProject.ext.bintrayUser
    key = rootProject.ext.bintrayPassword
    configurations = ['archives']
    override = false
    pkg {
        repo = 'maven'
        name = 'io.uuddlrlrba:android-dialer'
        licenses = ['MIT']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
    }
}
